#version 330 core

// The original: sleepless
// Created by diatribes on 2025-08-17
// https://www.shadertoy.com/view/WclcRN
// Audio reactive modifications by @OneHung

uniform float iTime;
uniform vec2 iResolution;
uniform sampler2D iChannel0;  // Audio texture

out vec4 fragColor;

// Audio reactive modifications:
// TWEAK THESE VALUES:
#define BASS_SAMPLE_POS 0.05    // Lower = deeper bass (0.01-0.1)
#define BASS_MULTIPLIER 4.0     // How much brighter on bass hits (2.0-8.0)
#define COLOR_SHIFT_AMOUNT 8.0  // How much color changes on bass (4.0-12.0)
#define BASS_THRESHOLD 0.1      // Minimum bass level to trigger (0.05-0.3)
#define ORB_BRIGHTNESS 0.5      // Base orb brightness (0.5-3.0)

// woke up and couldn't get back to sleep :/
void mainImage(out vec4 o, vec2 u) {
    float d = 0.0;
    float a = 0.0;
    float e = 0.0;
    float i = 0.0;
    float s = 0.0;
    float t = iTime;
    vec3  p = iResolution;
    
    // Get bass frequency from audio channel
    float bass = texture(iChannel0, vec2(BASS_SAMPLE_POS, 0.25)).x;
    bass = max(0.0, bass - BASS_THRESHOLD); // Remove noise floor
    float bassBoost = ORB_BRIGHTNESS * (1.0 + bass * BASS_MULTIPLIER); // Brightness multiplier
    
    // scale coords
    u = (u+u-p.xy)/p.y;
    
    // cinema bars
    if (abs(u.y) > .8) { o = vec4(0); return; }
    
    // camera movement
    u += vec2(cos(t*.4)*.3, cos(t*.8)*.1);
    
    o *= i;
    for(int iter = 0; iter < 128; iter++) {
        i++;

        // accumulate distance
        d += s = min(.01+.4*abs(s),e=max(.8*e, .01));

        // purple, blue color with audio reactive color shift
        o += (1.+cos(.1*p.z*vec4(3,1,0,0) + bass*COLOR_SHIFT_AMOUNT)) * bassBoost/(s+e*2.);

        // noise loop start, march
        p = vec3(u*d,d+t); // p = ro + rd *d, p.z + t;

        // entity (orb)
        e = length(p - vec3(
            sin(sin(t*.2)+t*.4) * 2.,
            1.+sin(sin(t*.5)+t*.2) *2.,
            12.+t+cos(t*.3)*8.))-.1;

        // spin by t, twist by p.z
        p.xy *= mat2(cos(.1*t+p.z/16.+vec4(0,33,11,0)));

        // mirrored planes 4 units apart
        s = 4. - abs(p.y);

        // noise starts at .42 up to 16., grow by a+=a
        for(a = .42; a < 16.; a += a) {
            // apply turbulence
            p += cos(.4*t+p.yzx)*.3;

            // apply noise
            s -= abs(dot(sin(.1*t+p * a ), .18+p-p)) / a;
        }
    }
    
    // tanh tonemap, brightness, light off-screen
    u += (u.yx*.9+.3-vec2(-1.,.5));
    o = tanh(o/6./max(dot(u,u), .001));
}

void main() {
    mainImage(fragColor, gl_FragCoord.xy);
}